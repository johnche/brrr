#!/usr/bin/env zsh
set -e

declare -r CWD="$(dirname $0)"
declare -r OPERATION="$1"
source "$CWD/brrr_common"

usage() {
	declare -A actions

	actions[install]="install packages"
	actions[list]="list installed packages"
	actions[update]="update package"
	actions[delete]="delete package"

	[[ "$#" > 0 ]] && echo "$1"
	echo "usage brrr <command> (<args>)\n"

	echo "Common commands"
	for key in "${(@k)actions}"; do
		printf "\t\e[0;33m%-25s\e[0m %s\n" "$key" "${actions[$key]}"
	done

	if [[ "$#" > 0 ]]; then
		exit 1
	else
		exit 0
	fi
}

init_brrr() {
	mkdir "$CWD/lib"
	mkdir "$CWD/bin"
	touch "$CWD/packages.txt"

	cat <<- EOF > $CWD/meta.txt
	ARCH="$(uname -m | tr '[:upper:]' '[:lower:]')"
	OS="$(uname -s | tr '[:upper:]' '[:lower:]')"
	EOF

	local -r this_file="$(readlink -f $ZSH_ARGZERO)"
	cat <<- EOF > $HOME/.local/bin/brrr
	#!/usr/bin/env zsh
	$this_file "\$@"
	EOF
	chmod u+x "$HOME/.local/bin/brrr"

	printer "init done"
}

if [[ ! -e "$CWD/meta.txt" || ! -e "$CWD/packages.txt" ]]; then
	printer "initializing brrr.."
	init_brrr
fi

[[ "$1" == '--help' || "$1" == '-h' ]] && usage
[[ "$#" == 0 ]] && usage "Missing command"

local -r CMD="$PWD/brrr_$OPERATION"
command -v "$CMD" > /dev/null || usage

shift 1
exec "$CMD" "$@"
